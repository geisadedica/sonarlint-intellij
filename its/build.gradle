#!groovy
// The above triggers groovy syntax highlighting in vim

plugins {
  id "org.jetbrains.intellij" version "0.4.20"
  id 'org.jetbrains.kotlin.jvm' version '1.3.71'
  id "java"
  id "com.github.hierynomus.license" version "0.14.0"
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'org.jetbrains.intellij'

group = 'org.sonarsource.sonarlint.intellij.its'
description = 'ITs for SonarLint IntelliJ'

sourceCompatibility = 1.8
targetCompatibility = 1.8

compileTestKotlin {
  kotlinOptions.jvmTarget = "1.8"
}

repositories {
  jcenter()
  mavenLocal()
  mavenCentral()
  maven { url = "https://jetbrains.bintray.com/intellij-third-party-dependencies" }
  maven {
    url "https://repox.jfrog.io/repox/sonarsource"
  }
}

if (!project.hasProperty("ijVersion")) {
  ext.ijVersion = '2020.1.1'
}

intellij {
  version ijVersion
  pluginName 'sonarlint-intellij-its'
  updateSinceUntilBuild false
  plugins = []
  if (project.hasProperty("slPluginLocation")) {
    plugins += slPluginLocation
  }
}

def remoteRobotVersion = "0.9.35"
def fixturesVersion = "1.1.18"

dependencies {
  testImplementation("com.intellij.remoterobot:remote-robot:$remoteRobotVersion")
  testImplementation("com.intellij.remoterobot:remote-fixtures:$fixturesVersion")
  testImplementation 'org.junit.jupiter:junit-jupiter-api:5.3.1'
  testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.3.1'
  testImplementation 'org.assertj:assertj-core:2.3.0'
  testImplementation 'org.mockito:mockito-core:1.10.19'
}

downloadRobotServerPlugin.version = remoteRobotVersion

runIdeForUiTests {
  systemProperty "robot-server.port", "8082"
  systemProperty 'sonarlint.telemetry.disabled', 'true'
}

test {
  useJUnitPlatform()
}

license {
  mapping {
    java = 'SLASHSTAR_STYLE'
  }
  strictCheck true
}


